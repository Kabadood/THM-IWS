package block_5._10_BuildskriptLesen

// https://docs.gradle.org/current/userguide/writing_tasks_intermediate.html

/**
 * Aufgabe 10
 * Ihr werdet nun von oben nach unten durcharbeiten und lernen, was alles mit Tasks in Gradle möglich ist, was die Vorteile sind.
 * Es werden Fragen gestellt, gebe deine Antworten nebendran an. Führe die Tasks auch aus.
 * Als erste Frage vielleicht um Warm zu werden:
 * - Wo kannst du hier ELemente von Groovy (Closures, GStrings, Methoden ohne Klammern, ...) erkennen?
 */

plugins {
    id 'groovy'
    id 'application'
}

group = 'de.thm.workshop'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:5.0.0'
    testImplementation 'org.spockframework:spock-core:2.4-groovy-5.0'
}

application {
    mainClass = 'block_1.Main'
}

tasks.register('helloTask') {
    description = "Gibt eine Begrüßung aus"
    group = "Workshop"

    doLast {
        println "Hello from Gradle"
        println "Projekt: $project.name"
        println "Version: $version"
    }
}

// Führe `./gradlew copyTask` aus
tasks.register('copyTask', Copy) {
    from("source")
    into("target")
    include("*.war")
}

tasks.register('removeOutput', Delete) {
    delete layout.buildDirectory.file("outputs/1.txt")
}

// Führe `./gradlew hello` aus

// Extend the DefaultTask class to create a HelloTask class
class HelloTask extends DefaultTask {
    @TaskAction
    void hello() {
        println("hello from HelloTask")
    }
}

// Register the hello Task with type HelloTask
tasks.register("hello", HelloTask) {
    group = "Custom tasks"
    description = "A lovely greeting task."
}

// Führe `./gradlew help --task hello` aus


// CreateAFileTask --> Now Gradle knows what the task needs and what it produces. If nothing changes, the task is skipped.
abstract class CreateAFileTask extends DefaultTask {
    @Input
    abstract Property<String> getFileText()

    @Input
    final String fileName = "myfile.txt"

    @OutputFile
    final File myFile = new File(fileName)

    @TaskAction
    void action() {
        myFile.createNewFile()
        myFile.text = fileText.get()
    }
}

// Führe `gradle -q intro` aus, funktioniert das? Warum nicht?
tasks.register('hello') {
    doLast {
        println 'Hello world!'
    }
}
tasks.register('intro') {
    // dependsOn tasks.hello
    doLast {
        println "I'm Gradle"
    }
}
// Füge `dependsOn tasks.hello` in `intro` hinzu, klappt es jetzt?

// Funktioniert `taskX` wenn man `taskY` erst danach definiert?
tasks.register('taskX') {
    dependsOn 'taskY'
    doLast {
        println 'taskX'
    }
}
tasks.register('taskY') {
    doLast {
        println 'taskY'
    }
}

// The hello task now depends on the assemble task, which means that Gradle must execute the assemble task before it can execute the hello task:
//`./gradlew :app:hello`
tasks.register('hello') {
    group = "Custom"
    description = "A lovely greeting task."
    doLast {
        println("Hello world!")
    }
    dependsOn(tasks.assemble)
}

// Wir können mehrere Nacheinander ausführen, was passiert bei mehreren "doLast"?
//(The calls doFirst and doLast can be executed multiple times. They add an action to the beginning or the end of the task’s actions list. When the task executes, the actions in the action list are executed in order. )
tasks.register('hello') {
    doLast {
        println 'Hello Earth'
    }
}
tasks.named('hello') {
    doFirst {
        println 'Hello Venus'
    }
}
tasks.named('hello') {
    doLast {
        println 'Hello Mars'
    }
}
tasks.named('hello') {
    doLast {
        println 'Hello Jupiter'
    }
}